10 REM CONRAD GAME OF LIFE FOR B3 ,BY D  .MURRAY 2023
20 REM ANY LIVE CELL WITH FEWER THAN TWO LIVE NEIGHBORS DIES ,AS IF BY UNDERPOPULATION  .
30 REM ANY LIVE CELL WITH TWO OR THREE LIVE NEIGHBORS LIVES ON TO THE NEXT GENERATION  .
40 REM ANY LIVE CELL WITH MORE THAN THREE LIVE NEIGHBORS DIES ,AS IF BY OVERPOPULATION  .
50 REM ANY DEAD CELL WITH EXACTLY THREE LIVE NEIGHBORS BECOMES A LIVE CELL ,AS IF BY REPRODUCTION  .
60 DISPLAY 200:IMMEDIATE  0 : BORDER 0
70 COLOR  1, 9: CLS
80 W =30:H =20 : XO = INT((COLS()-W ) / 2): YO = INT((ROWS()-H ) / 2): 
90 DIM CELLS (W ,H ):DIM CELLS2 (W ,H )
100 FOR Y = 0TO H - 1 : FOR X = 0TO W - 1
120     CELLS (X ,Y )= 0
130 NEXT X : NEXT Y
150 CENTERX =25:CENTERY =12
155  X= centerX+1   : Y=centerY  : CELLS (X ,Y ) = 1
160  X= centerX+2   : Y=centerY  : CELLS (X ,Y ) = 1
165  X= centerX     : Y=centerY+1: CELLS (X ,Y ) = 1
170  X= centerX+1   : Y=centerY+1: CELLS (X ,Y ) = 1
175  X= centerX+1   : Y=centerY+2: CELLS (X ,Y ) = 1

199 SUB MAIN
200 REM LOOP
210 GOSUB COPYCELLS
220 FOR Y = 0TO H - 1
230 FOR X = 0TO W - 1

240   LIVENEIGHBORS = 0
241   IF Y > 0THEN LIVENEIGHBORS =LIVENEIGHBORS +CELLS (X ,Y - 1)
242   IF Y <H - 1THEN LIVENEIGHBORS =LIVENEIGHBORS +CELLS (X ,Y + 1)
243   IF X > 0THEN LIVENEIGHBORS =LIVENEIGHBORS +CELLS (X - 1,Y )
244   IF X <W - 1THEN LIVENEIGHBORS =LIVENEIGHBORS +CELLS (X + 1,Y )
245   IF Y > 0AND X > 0THEN LIVENEIGHBORS =LIVENEIGHBORS +CELLS (X - 1,Y - 1)
246   IF Y > 0AND X <W - 1THEN LIVENEIGHBORS =LIVENEIGHBORS +CELLS (X + 1,Y - 1)
247   IF Y <H - 1AND X > 0THEN LIVENEIGHBORS =LIVENEIGHBORS +CELLS (X - 1,Y + 1)
248   IF Y <H - 1AND X <W - 1THEN LIVENEIGHBORS =LIVENEIGHBORS +CELLS (X + 1,Y + 1)

250   IF CELLS (X ,Y )= 1AND LIVENEIGHBORS < 2THEN CELLS2 (X ,Y )= 0
270   IF CELLS (X ,Y )= 1AND LIVENEIGHBORS > 3THEN CELLS2 (X ,Y )= 0
280   IF CELLS (X ,Y )= 0THEN IF LIVENEIGHBORS = 3THEN CELLS2 (X ,Y )= 1
290 NEXT X
300 NEXT Y
310 WAITMS 20
320 GOSUB COPYCELLSBACK
330 GOTO 200

7000 SUB COPYCELLS
7010 FOR Y = 0TO H - 1
7020 FOR X = 0TO W - 1
7030 CELLS2 (X ,Y )=CELLS (X ,Y )
7040 NEXT X
7050 NEXT Y
7060 RETURN

8000 SUB COPYCELLSBACK
8010 FOR Y = 0TO H - 1
8020 FOR X = 0TO W - 1
8030 V =CELLS2 (X ,Y ):CELLS (X ,Y )=V
8040 IF V = 0THEN POKECCL XO+X ,YO+Y ,259, 0, 10
8050 IF V = 1THEN POKECCL XO+X ,YO+Y ,259, 10, 1
8060 NEXT X
8070 NEXT Y
8080 RETURN